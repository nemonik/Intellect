from intellect.examples.bahBahBlackSheep.BlackSheep import BlackSheep
from intellect.examples.bahBahBlackSheep.BuyOrder import BuyOrder
import logging
import random

'''
Ruleset based on the nursery rhyme...
'''

rule "Start policy":
	then:
		log("Policy is being reasoned over.", "example", logging.DEBUG)

rule "Baa, baa, black sheep, Have you any wool?":
	when:
		exists BlackSheep()
	then:
		print( "Me:  Baa, baa, black sheep, Have you any wool?" )

rule "Have you no bags full?":
	when: 
		$sheep := BlackSheep( len(bags_of_wool) == 0 )
	then:
		print( "Sheep {0}: No, sir, No, sir.".format( $sheep.name ) )

rule "At least 1 bag full?":
	when:
		$sheep := BlackSheep( len(bags_of_wool) >= 1 )
	then:
		print( "{0}: Yes, sir, yes, sir,".format( $sheep.name ) )
		print( "{0}: {1} bags full".format( $sheep.name, len($sheep.bags_of_wool) ) )
		print( "{0}: One for my master,".format( $sheep.name ) )

rule "At least 2 bags full?":
	when:
		$sheep := BlackSheep( len(bags_of_wool) >= 2 )
	then:
		print("{0}: And one for my dame,".format( $sheep.name ) )

rule "3 bags full?":
	when:
		$sheep := BlackSheep( len(bags_of_wool) == 3 )
	then:
		print( "{0}: and one for the little boy".format( $sheep.name ) )
		print( "{0}: Who lives down the lane.".format( $sheep.name ) )

rule "Retire the sheep and insert a buy order":
	when:
		$sheep := BlackSheep( len(bags_of_wool) == 3 )
	then:
		print( "Retiring sheep {0}.".format( $sheep.name ) )
		forget $sheep
		insert BuyOrder(random.randint(1, 1))

rule "Time to buy new sheep?":
	when:
		$buyOrder := BuyOrder( )
	then:
		modify $buyOrder:
			count = $buyOrder.count - 1
		learn BlackSheep()

rule "Remove empty buy orders":
	when:
		$buyOrder := BuyOrder( count == 0 )
	then:
		forget $buyOrder

rule "End policy":
	then:
		log("Finished reasoning over policy.", "example", logging.DEBUG)
		halt
